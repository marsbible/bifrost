/**
 * TODO: 需要关注mleap的重大更新，需要关注其对scala 2.12的支持
 */
plugins {
    id 'java'
    id 'scala'
    id 'idea'
    id 'application'
    id "com.lightbend.akka.grpc.gradle" version "0.4.2" apply false
}
group 'bifrost'
version '0.1'
sourceCompatibility = 1.8
mainClassName = 'org.bifrost.worker.Main'

configurations {
    alpnagent
}

configurations.all {
    exclude group:'com.lightbend.akka.grpc', module: 'akka-grpc-runtime_2.12'
    exclude  group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude  group: 'log4j', module: 'log4j'
    resolutionStrategy.force 'org.apache.zookeeper:zookeeper:3.4.13'
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.forkOptions.with {
        memoryMaximumSize = '8g'
        jvmArgs = ['-Xss16M']
    }
}

task genProto {
    //com.lightbend.akka.grpc.gradle在windows上有bug
    if (!org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        apply plugin: 'com.lightbend.akka.grpc.gradle'
        akkaGrpc {
            language = "Scala"
            generateClient = true
            generateServer = true
        }
    }
}


repositories {
    flatDir {
        dirs 'libs'
    }
    jcenter()
    //mavenCentral()
}

sourceSets.main.scala.srcDirs = ['src/main/scala', 'build/generated/source/proto/main/akkaGrpc', 'build/generated/source/proto/main/scalapb']
sourceSets.main.java.srcDirs = ['src/main/java']

dependencies {
    //predict framework
    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        compile 'com.esotericsoftware.kryo:kryo:2.21'
        compile name: 'xgboost4j-0.81-win64'
    }
    else if(org.gradle.internal.os.OperatingSystem.current().isLinux()) {
        compile 'com.esotericsoftware.kryo:kryo:2.21'
        // 为在线预测优化的版本，参见http://git.xin.com/recommend/xgboost
        compile name: 'xgboost4j-0.81-linux-online'
    }
    else {
        compile group: 'ml.dmlc', name: 'xgboost4j', version: '0.81'
    }

    compile "ml.combust.mleap:mleap-runtime_2.11:0.13.0"
    //mlep-xgboost-runtime已经集成到worker代码中，为了增加批处理功能
    //compile("ml.combust.mleap:mleap-xgboost-runtime_2.11:0.13.0")

    //akka
    compile group: 'com.typesafe.akka', name: 'akka-http_2.11',   version: '10.1.6'
    compile group: 'com.typesafe.akka', name: 'akka-http-spray-json_2.11', version: '10.1.6'
    compile group: 'com.typesafe.akka', name: 'akka-stream_2.11', version: '2.5.19'
    compile group: 'com.lightbend.akka.grpc', name: 'akka-grpc-runtime_2.11', version: '0.4.2'
    compile group: 'com.thesamet.scalapb', name: 'scalapb-json4s_2.11', version: '0.7.2'

    //io library
    compile group: 'io.netty', name: 'netty-all', version: '4.1.32.Final'
    compile group: 'org.apache.hbase', name: 'hbase-shaded-client', version: '1.2.9'
    compile group: 'mysql', name: 'mysql-connector-java', version: '8.0.13'
    compile group: 'com.typesafe.slick', name: 'slick_2.11', version: '3.2.3'
    compile group: 'com.typesafe.slick', name: 'slick-hikaricp_2.11', version: '3.2.3'
    compile group: 'org.apache.solr', name: 'solr-solrj', version: '7.6.0'
    compile group: 'org.mongodb.scala', name: 'mongo-scala-driver_2.11', version: '2.5.0'
    compile group: 'com.github.Ma27', name: 'rediscala_2.11', version: '1.8.4'
    compile group: 'com.sksamuel.elastic4s', name: 'elastic4s-http_2.11', version: '6.5.0'
    compile group: 'com.sksamuel.elastic4s', name: 'elastic4s-spray-json_2.11', version: '6.5.0'

    //others
    compile group: 'org.scalatra.scalate', name: 'scalate-core_2.11', version: '1.9.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.11.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: '2.11.1'
    alpnagent 'org.mortbay.jetty.alpn:jetty-alpn-agent:2.0.7'
    compile group:"com.typesafe.akka", name: "akka-slf4j_2.11", version: "2.5.19"
}

jar {
    zip64 true
    archiveName = "bifrost-worker.jar"

    manifest {
        attributes 'Main-Class': 'org.bifrost.worker.Main'
        attributes 'Class-Path': configurations.runtime.files.collect { "libs/$it.name" }.join(' ')
    }

    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

task copyToLib(type: Copy) {
    into "$buildDir/output/libs"
    from configurations.runtime
}

build.dependsOn(copyToLib)
